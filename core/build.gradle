import org.gradle.internal.os.OperatingSystem

plugins {
    id 'java-library'
}

project.ext.lwjglVersion = "3.3.2-SNAPSHOT"
project.ext.slf4jVersion = "2.0.0-beta1"
project.ext.jomlVersion = "1.10.4"
def resourcesPath = "../resources"

switch (OperatingSystem.current()) {
    case OperatingSystem.LINUX:
        project.ext.lwjglNatives = "natives-linux"
        break
    case OperatingSystem.MAC_OS:
        project.ext.lwjglNatives = System.getProperty("os.arch").startsWith("aarch64") ? "natives-macos-arm64" : "natives-macos"
        break
    case OperatingSystem.WINDOWS:
        def osArch = System.getProperty("os.arch")
        project.ext.lwjglNatives = osArch.contains("64")
                ? "natives-windows${osArch.startsWith("aarch64") ? "-arm64" : ""}"
                : "natives-windows-x86"
        break
}

sourceSets {
    main {
        resources {
            srcDir(resourcesPath)
        }
    }
}

logger.quiet("[ENGINE] Natives in use: ${project.ext.lwjglNatives}")
logger.quiet("[ENGINE] Resources path: $resourcesPath")

dependencies {
    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-opengl"
    implementation "org.lwjgl:lwjgl::$lwjglNatives"
    implementation "org.lwjgl:lwjgl-glfw::$lwjglNatives"
    implementation "org.lwjgl:lwjgl-opengl::$lwjglNatives"
    implementation "org.slf4j:slf4j-api:$slf4jVersion"
    implementation "org.slf4j:slf4j-simple:$slf4jVersion"
    api "org.joml:joml:$jomlVersion"
}
