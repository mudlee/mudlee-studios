plugins {
    id 'java'
    id 'application'
    id 'org.beryx.jlink' version '2.25.0'
}

dependencies {
    implementation project(":core")
}

def os = System.properties['os.name'].toLowerCase()
var engineJvmArgs = ["-Dorg.lwjgl.system.allocator=system","-Dorg.lwjgl.util.DebugLoader=true" , "-Dorg.lwjgl.util.Debug=true" , "-Dorg.lwjgl.opengl.Display.enableHighDPI=true" , "-Dorg.lwjgl.opengl.Display.enableOSXFullscreenModeAPI=true"]

if (os.contains('mac')) {
    engineJvmArgs.add("-XstartOnFirstThread")
}

application {
    setMainModule("hu.mudlee.sandbox")
    setMainClass("hu.mudlee.sandbox.SandboxApplication")
    setApplicationDefaultJvmArgs(engineJvmArgs)

    logger.quiet("[ENGINE] [application] Main Module: ${mainModule.get()}")
    logger.quiet("[ENGINE] [application] Main Class: ${mainClass.get()}")
    logger.quiet("[ENGINE] [application] JVM args: $engineJvmArgs")

    modularity {
        // See https://github.com/java9-modularity/gradle-modules-plugin/issues/165
        disableEffectiveArgumentsAdjustment()
    }
}

jlink {
    addOptions("--strip-debug", "--compress", "2", "--no-header-files", "--no-man-pages")
    // https://github.com/beryx-gist/badass-jlink-example-richtextfx/blob/master/build.gradle
    // jvmArgs = ['-splash:$APPDIR/splash.png']

    logger.quiet("[ENGINE] [jlink] Options: ${jlink.options.get()}")
    logger.quiet("[ENGINE] [jlink] JVM Args: $engineJvmArgs")

    launcher {
        name = "sandbox-app"
        jvmArgs = engineJvmArgs
    }

    jpackage {
        skipInstaller = true
    }
}

